plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "0.12.0-SNAPSHOT" apply false
    id "com.matthewprenger.cursegradle" version "1.4.0" apply false
}

architectury {
    minecraft = rootProject.minecraft_version
}

subprojects {
    apply plugin: "dev.architectury.loom"
    apply from: "https://files.latmod.com/public/markdown-git-changelog.gradle"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
        // The following line declares the mojmap mappings, you may use other mappings as well
        mappings loom.officialMojangMappings()
        // The following line declares the yarn mappings you may select this one as well.
        // mappings "net.fabricmc:yarn:1.19+build.2:v2"
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"
    apply plugin: 'com.matthewprenger.cursegradle'

    def ENV = System.getenv()

    archivesBaseName = rootProject.archives_base_name
    version = "${mod_version}-build.${ENV.GITHUB_RUN_NUMBER ?: 9999}+mc${minecraft_version}"
    group = rootProject.maven_group

    repositories {
        maven {
            url "https://maven.saps.dev/releases"
            content {
                includeGroup "dev.ftb.mods"
                includeGroup "dev.latvian.mods"
            }
        }
        maven { url 'https://jitpack.io'
            content {
                includeGroup "com.github.cbyrneee"
            }
        }
        maven {
            url "https://maven.blamejared.com"
            content {
                includeGroup "com.faux.ingredientextension"
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
        options.release = 17
    }

    java {
        withSourcesJar()
    }
}

task curseforgePublish